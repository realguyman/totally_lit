plugins {
    id "fabric-loom" version "1.0-SNAPSHOT"
    id "com.modrinth.minotaur" version "2.+"
    id "maven-publish"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://maven.wispforest.io" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft}"
    mappings "net.fabricmc:yarn:${project.mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api}"
    modLocalRuntime "com.terraformersmc:modmenu:${project.mod_menu}"
    annotationProcessor modImplementation("io.wispforest:owo-lib:${project.owo_lib}")
}

loom {
    splitEnvironmentSourceSets()

    mods {
        totally_lit {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

    runs {
        gametest {
            inherit server
            name "Gametest"
            vmArg "-Dfabric-api.gametest"
            vmArg "-Dfabric-api.gametest.report-file=${project.buildDir}/junit.xml"
            runDir "build/gametest"
        }
    }
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

import com.modrinth.minotaur.dependencies.ModDependency

modrinth {
    if (project.version.contains("-alpha")) {
        versionType = "alpha"
    } else if (project.version.contains("-beta")) {
        versionType = "beta"
    }

    projectId = "hbKDMT6l"
    uploadFile = remapJar
    additionalFiles = [remapSourcesJar]
    gameVersions = [project.minecraft]
    dependencies = [
            new ModDependency("P7dR8mSH", "required"),
            new ModDependency("mOgUt4GM", "optional")
    ]
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
